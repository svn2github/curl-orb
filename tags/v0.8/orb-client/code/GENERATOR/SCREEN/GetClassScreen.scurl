||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp. All Rights Reserved.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

|||
||| @author  ucai zhang, helped by Lei Zhao
|||

|||
||| GetClassScreen
||| The class create the class seletion class name
{define-class GetClassScreen {inherits AbstractPanel}
  
  field private _body:#Graphic
  field private _dl-class:#CustomDropdownList
  field private _cancel-button:#CustomCommandButton
  field private _next-button:#CustomCommandButton
  field private _back-button:#CustomCommandButton
  field private data:#{HashTable-of any, any}
  field private _from-screen:Dynamic = {Dynamic ""}
  
  {constructor {default ...}
    {construct-super
        {splice ...}
    }
    {self.layout-graphic false}
  }

  {method public {handle-data transfer-data:any}:bool
    {if {equal? transfer-data, null} then
        {return false}
    }    
    {self._dl-class.clear-items}
    set self.data = transfer-data asa {HashTable-of any, any}
    {for val in {self.data.get "data"} asa {Array-of #String} do
        def item = {ListValueItem val}
        set item.width = 132pt
        set item.tooltip = {Tooltip val}
        {self._dl-class.append item}
    }
    {if self._dl-class.data-model.size > 0 then
        {self._dl-class.select-index 0}
        set self._dl-class.tooltip = {Tooltip self._dl-class.value}
    }
    set self._from-screen.value = {self.data.get "from-screen"} asa String
    
    {return true}
  }
  
  {setter public {body body:#Graphic}:void
    set self._body = body
  }

  {getter public {body}:Graphic
    {if {equal? self._body , null} then
        || screen body        
        set self._body =
            {VBox
                {Fill height = 12pt},
                {HBox
                    spacing = 5pt,
                    valign = "center",
                    font-family = "Arial Rounded MT Bold" ,
                    color = FONT-COLOR,||"white",
                    font-weight = "bold",
                    font-size = 12pt,
                    {Fill width = 30pt},
                    {hrule
                        height = 1pt,
                        width = 30pt,
                        color = FONT-COLOR|#"white"#|
                    },
                    self._from-screen,
                    font-size = 15pt
                },
                
                {HBox
                    width = 15cm,
                    height = 4.5cm,
                    spacing = 10pt,
                    {Frame
                        width = 100pt,
                        height = 10pt,
                        halign = "right",
                        valign = "center",                                     
                        {create-sepc-label "Class name"},
                        color = "black",font-size = 13pt
                    },
                    {Frame                     
                        valign = "center",
                        self.classDropdownList
                    }
                    
                    
                },
                {Fill height = 25.2pt},
                {hrule height = 1pt, color = "#ACA899"},
                {Fill height = .2cm},
                {HBox
                    valign = "top",
                    spacing = 5pt,
                    self.backButton,
                    {Fill},
                    self.nextButton,
                    self.cancelButton
                    
                }
            }
        
    }
    {return {non-null self._body}}
  }

  || cancel button setting
  {getter public {cancelButton}:CustomCommandButton
    {if{equal? self._cancel-button, null} then
        set self._cancel-button =
            {CustomCommandButton
                label = "Cancel",
                {on Action do
                    {{self.get-view}.close}
                }
            }
    }
    {return {non-null self._cancel-button}}
  }

  {getter public {backButton}:CustomCommandButton
    {if{equal? self._back-button, null} then
        set self._back-button =
            {CustomCommandButton
                label = "Back",
                {on Action do
                    {with-busy-cursor
                        {change-body {ClassSelectionPanel}}
                    }
                }
            }
    }
    {return {non-null self._back-button}}
  }

  || cancel button setting
  {getter public {nextButton}:CustomCommandButton
    {if{equal? self._next-button, null} then
        set self._next-button =
            {CustomCommandButton
                label = "Next" ,
                {on Action do                      
                    {if{equal? self._dl-class.value , null } then
                        {ErrorMessages.get-error-messages
                            ErrorMessages.DROPDOWNLIST-ERROR-MESSAGE
                        }
                     else
                        {try
                            {with-busy-cursor
                                def server-data =
                                    {ClassPropertyLoader.get-ClassProperty-in-ApplicationContext self._dl-class.value asa String,server-url = server-url}
                                {if server-data.interface-properties == null or server-data.interface-properties.size == 0
                                 then
                                    {change-body
                                        {ClassModifierPanel
                                            server-data asa ClassProperty,
                                            from-screen = self._from-screen.value asa String
                                        },
                                        transfer-data = self._from-screen.value,
                                        width = 765pt,
                                        height = 535pt,
                                        add? = false
                                    }
                                 else
                                    def tranfer-data = {HashTable}
                                    {tranfer-data.set "from-screen", self._from-screen.value}
                                    {tranfer-data.set "interfaces", server-data.interface-properties}
                                    {tranfer-data.set "class-property", server-data}
                                    {change-body
                                        {InterfacesSelectionScreen},
                                        transfer-data = tranfer-data
                                    }
                                }
                            }
                         catch obj:ORBServerException  do
                            {ErrorMessages.get-error-messages
                                ErrorMessages.CLASS-REQUEST-EXCEPTION
                            }
                         catch ht:HttpException do
                            {ErrorMessages.get-error-messages
                                ErrorMessages.SERVER-EXCEPTION
                            }
                         catch se:SerializeException do
                            {ErrorMessages.get-error-messages
                                ErrorMessages.SERIALIZE-EXCEPTION
                            }
                         catch obj:Exception  do
                            {ErrorMessages.get-error-messages
                                ErrorMessages.UNEXPECTED-EXCEPTION
                            }
                        }
                    }
                }
            }
    }
    {return {non-null self._next-button}}
  }

  {getter public {classDropdownList}:#CustomDropdownList
    {if{equal? self._dl-class, null} then
        set self._dl-class =
            {CustomDropdownList                                    
                height = 22pt,
                width = 160pt,
                font-size = 12pt,
                dropdown-height = 60pt,
                {on ValueFinished at dd:CustomDropdownList do
                    {if dd.selected-item.value != "" and dd.selected-item.value != null then
                        set self._dl-class.tooltip = {Tooltip dd.selected-item.value}
                        
                    }
                }
            }
    }
    {return {non-null self._dl-class}}
  }
  
  {method protected {layout-graphic header?:bool}:void
    {self.add-internal
        self.body,
        replace? = true
    }
  }
}
