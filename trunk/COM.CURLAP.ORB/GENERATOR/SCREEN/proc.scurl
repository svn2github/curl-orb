||| Copyright (C) 1998-2008, Sumisho Computer Systems Corp. All Rights Reserved.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

|||
||| @author ucai zhang
|||

{define-proc {change-body
                 body:AbstractPanel,
                 transfer-data:any = null,
                 width:any = 400pt,
                 height:any = 250pt,
                 add?:bool = true
             }:bool
    def parent-vv = {CodeGeneratorControl.get-instance}.code-generator-screen
    def parent-v = {CodeGeneratorControl.get-instance}.main-view

    let (x:Distance, y:Distance) = {parent-v.get-window-position}
    let pixel-size:Distance = parent-v.display-context.pixel-size
    let (left:int, top:int, width-p:int, height-p:int) =
        {parent-v.display-context.get-bounds-in-pixels}
    let width1:Distance = width-p * pixel-size
    let height1:Distance = height-p * pixel-size
    let x1:Distance = 0m
    let y1:Distance = 0m
    let r-width:Distance = width asa Distance
    let r-height:Distance = height asa Distance
    {if r-width > 0pt and r-height > 0pt then
        {if (x < width1/2 and (x + r-width/2) != width1/2) or
            ( x > width1/2 and (x - r-width/2) != width1/2)
         then
            set x1 = width1/2 - r-width/2 - 3pt
        }
        {if (y < height1/2 and (y + r-height/2) != height1/2 ) or
            (y > height1/2 and (y - r-height/2) != height1/2 )
         then
            set y1 = height1/2 - r-height/2  -  20pt
        }
    }

    set parent-vv.width = width
    set parent-vv.height = height

    {parent-v.set-size-from-child}
    {parent-v.set-position x1 , y1}
    
    {return
        {parent-vv.change-body
            body,
            transfer-data = transfer-data,
            add? = add?
        }
    }
}

{define-proc {change-dialog
                 v:AbstractDialog,
                 transfer-data:any = null,
                 title:String = ""
             }:bool
    def parent-vv =
        {CodeGeneratorControl.get-instance}.code-generator-screen
    {return
        {parent-vv.change-dialog
            v,
            transfer-data = transfer-data,
            title = title
        }
    }
}

{define-proc {create-sepc-label str-value:String}:any
    {return
        {text color = FONT-COLOR|#"white"#|, font-size = 12pt, {value str-value}}
    }
}

{define-proc {change-to-curl-style
                 value:String
             }:String
    {return {ClassBuilderUtil.marshal-property value}}
}

{define-proc {add-sharp
                 value:String
             }:String
    def v = {value.trim-clone}
    {return
        {switch v
         case
            "char", "bool", "int", "int8", "int16", "int32", "int64",
            "byte", "double", "float", "void", "any", ""
         do
            value 
         else
            "#" & v
        }
    }
}

